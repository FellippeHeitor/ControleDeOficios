': This program was generated by
': InForm - GUI system for QB64 - Beta version 8
': Fellippe Heitor, 2016-2018 - fellippe@qb64.org - @fellippeheitor
'-----------------------------------------------------------

'Icon: http://www.iconarchive.com/show/keyboard-keys-icons-by-chromatix/hash-icon.html
'Artist: Chromatix
'Iconset: Keyboard Keys Icons (102 icons)
'License: CC Attribution-Noncommercial-No Derivate 4.0
'Commercial usage: Not allowed

$VERSIONINFO:FILEVERSION#=0,0,0,3
$VERSIONINFO:PRODUCTVERSION#=0,0,0,3
$VERSIONINFO:CompanyName=Fellippe Heitor
$VERSIONINFO:FileDescription=Controle De Oficios TJMG
$VERSIONINFO:FileVersion=0.3b
$VERSIONINFO:InternalName=ControlDeOficios.bas
$VERSIONINFO:LegalCopyright=Open source
$VERSIONINFO:OriginalFilename=ControlDeOficios.exe
$VERSIONINFO:ProductName=Controle De Oficios TJMG
$VERSIONINFO:ProductVersion=0.3b
$VERSIONINFO:Comments=Made with QB64.

': Controls' IDs: ------------------------------------------------------------------
DIM SHARED ControleDeOficios AS LONG
DIM SHARED Frame1 AS LONG
DIM SHARED PrximoNmero AS LONG
DIM SHARED ltimoUtilizadoLB AS LONG
DIM SHARED UltimoOficioLB AS LONG
DIM SHARED Label2 AS LONG
DIM SHARED UltimaDescricaoLB AS LONG
DIM SHARED UltimaDescricaoTB AS LONG
DIM SHARED Label3 AS LONG
DIM SHARED UltimoUsuarioLB AS LONG
DIM SHARED UltimoUsuarioTB AS LONG
DIM SHARED Label4 AS LONG
DIM SHARED UsuarioAtualLB AS LONG
DIM SHARED DecriaoOpcionalLB AS LONG
DIM SHARED DescricaoTB AS LONG
DIM SHARED BT AS LONG
DIM SHARED FirstBT AS LONG, PreviousBT AS LONG
DIM SHARED NextBT AS LONG, LastBT AS LONG
DIM SHARED MenuBar1 AS LONG
DIM SHARED MenuItem1 AS LONG
DIM SHARED MenuBar2 AS LONG
DIM SHARED MenuItem2 AS LONG
DIM SHARED MenuItem3 AS LONG
DIM SHARED Label10 AS LONG
DIM SHARED UltimoOficioTB AS LONG
DIM SHARED ClearSearchBT AS LONG
DIM SHARED CopyMenu AS LONG
DIM SHARED CopyMenuCopy AS LONG
DIM SHARED StatusBarLB AS LONG

DIM SHARED Ultimo AS _UNSIGNED LONG, Proximo AS _UNSIGNED LONG
DIM SHARED UltimoAno AS INTEGER, TotalRecords AS _UNSIGNED LONG
DIM SHARED CurrentRecord AS _UNSIGNED LONG
DIM SHARED EsteAno AS INTEGER
DIM SHARED Usuario AS STRING
DIM SHARED LastCheck AS SINGLE
DIM SHARED file$, backupFile$, GenericUser AS STRING
DIM SHARED inSearch AS _BYTE, totalFound AS _UNSIGNED LONG, searchResultIndex AS _UNSIGNED LONG
DIM SHARED Searching AS _BYTE, SearchProgress AS _UNSIGNED LONG
DIM SHARED SearchString$, DoLocalBackup AS _BYTE, DoingLocalBackup AS _BYTE
DIM SHARED FinishBackupAndClose AS _BYTE
REDIM SHARED SearchResults(0) AS _UNSIGNED LONG

file$ = "oficios.ini"
backupFile$ = ENVIRON$("USERPROFILE") + "\oficios-backup.ini"
Usuario = "Secretaria Criminal"
GenericUser = Usuario
IF LEN(COMMAND$(1)) THEN Usuario = COMMAND$(1)

EsteAno = VAL(RIGHT$(DATE$, 4))

DIM i AS INTEGER
FOR i = 1 TO _COMMANDCOUNT
    SELECT CASE LCASE$(COMMAND$(i))
        CASE "-civel"
            file$ = "oficios-civel.ini"
            backupFile$ = ENVIRON$("USERPROFILE") + "\oficios-civel-backup.ini"
            Usuario = COMMAND$(1)
            GenericUser = "Secretaria Cível"
            EXIT FOR
        CASE "-arquivo"
            file$ = COMMAND$(i + 1)
            backupFile$ = ENVIRON$("USERPROFILE") + "\" + file$ + "-backup.ini"
            file$ = file$ + ".ini"
        CASE "-usuario"
            Usuario = COMMAND$(i + 1)
        CASE "-setor"
            GenericUser = COMMAND$(i + 1)
    END SELECT
NEXT

': External modules: ---------------------------------------------------------------
'$INCLUDE:'..\InForm\InForm\InForm.ui'
'$INCLUDE:'..\InForm\InForm\xp.uitheme'
'$INCLUDE:'..\INI-Manager\ini.bm'
'$INCLUDE:'ControleDeOficios.frm'

': Event procedures: ---------------------------------------------------------------
SUB __UI_BeforeInit

END SUB

SUB __UI_OnLoad
    SetFrameRate 250

    IniSetForceReload True

    a$ = ReadSetting(file$, "controle", "registros")
    IF LEN(a$) > 0 THEN
        TotalRecords = VAL(a$)
        REDIM SearchResults(1 TO TotalRecords) AS _UNSIGNED LONG
        CurrentRecord = TotalRecords
    END IF

    a$ = ReadSetting(file$, "controle", "UltimoNumero")
    IF LEN(a$) > 0 THEN
        Ultimo = VAL(LEFT$(a$, INSTR(a$, "/") - 1))
        UltimoAno = VAL(MID$(a$, INSTR(a$, "/") + 1))
        Caption(UltimoOficioLB) = a$
        IF UltimoAno < EsteAno THEN
            Proximo = 1
        ELSE
            Proximo = Ultimo + 1
        END IF
        Refresh
    ELSE
        WriteSetting file$, "controle", "UltimoNumero", "0/0"
        Caption(UltimoOficioLB) = "-"
        Caption(UltimaDescricaoLB) = "-"
        Caption(UltimoUsuarioLB) = "-"
        Proximo = 1
    END IF

    Caption(ControleDeOficios) = "Controle de Ofícios - " + GenericUser
    Caption(BT) = LTRIM$(STR$(Proximo))
    Caption(Label10) = GenericUser
    Caption(UsuarioAtualLB) = Usuario
    __UI_Focus = DescricaoTB
    __UI_DefaultButtonID = BT
    DoLocalBackup = True
    LastCheck = TIMER
END SUB

SUB __UI_BeforeUpdateDisplay
    Control(FirstBT).Disabled = False
    Control(PreviousBT).Disabled = False
    Control(NextBT).Disabled = False
    Control(LastBT).Disabled = False

    IF TIMER - LastCheck > 1 THEN
        Refresh
    END IF

    DIM a$, b$, c$
    STATIC LocalBackupStarted AS _BYTE
    STATIC totalRecordsToBackup AS _UNSIGNED LONG
    STATIC backupIndex AS _UNSIGNED LONG
    STATIC lastRecordBackedUp AS _UNSIGNED LONG

    IF DoLocalBackup THEN
        DoLocalBackup = False
        DoingLocalBackup = True
        LocalBackupStarted = False
    END IF

    IF DoingLocalBackup THEN
        'backups are incremental
        IF LocalBackupStarted = False THEN
            a$ = ReadSetting(file$, "controle", "UltimoNumero")
            b$ = ReadSetting(file$, "controle", "registros")
            WriteSetting backupFile$, "controle", "UltimoNumero", a$
            WriteSetting backupFile$, "controle", "registros", b$
            IF lastRecordBackedUp > 0 THEN backupIndex = lastRecordBackedUp ELSE backupIndex = 1
            totalRecordsToBackup = VAL(b$)
            LocalBackupStarted = True
        END IF

        BackupNextItem:
        Caption(StatusBarLB) = "Salvando backup local (" + LTRIM$(STR$(INT(backupIndex / totalRecordsToBackup * 100))) + "%)..."

        num$ = ReadSetting(file$, STR$(backupIndex), "Numero")
        a$ = ReadSetting(file$, STR$(backupIndex), "Descricao")
        b$ = ReadSetting(file$, STR$(backupIndex), "Data")
        c$ = ReadSetting(file$, STR$(backupIndex), "Usuario")
        IF LEN(num$) > 0 OR LEN(a$) > 0 OR LEN(b$) > 0 OR LEN(c$) > 0 THEN
            IF LEN(num$) THEN WriteSetting backupFile$, STR$(backupIndex), "Numero", num$
            IF LEN(a$) THEN WriteSetting backupFile$, STR$(backupIndex), "Descricao", a$
            IF LEN(b$) THEN WriteSetting backupFile$, STR$(backupIndex), "Data", b$
            IF LEN(c$) THEN WriteSetting backupFile$, STR$(backupIndex), "Usuario", c$
            lastRecordBackedUp = backupIndex
        END IF

        backupIndex = backupIndex + 1
        IF backupIndex > totalRecordsToBackup THEN
            IF FinishBackupAndClose THEN SYSTEM
            DoingLocalBackup = False
            LocalBackupStarted = False
        ELSE
            IF FinishBackupAndClose THEN
                _LIMIT 1000
                GOTO BackupNextItem
            END IF
        END IF
    ELSE
        Caption(StatusBarLB) = "Pronto."
        Control(StatusBarLB).Redraw = True
        IF FinishBackupAndClose THEN SYSTEM
    END IF

    IF inSearch THEN
        Control(FirstBT).ForeColor = _RGB32(0, 0, 255)
        Control(PreviousBT).ForeColor = _RGB32(0, 0, 255)
        Control(NextBT).ForeColor = _RGB32(0, 0, 255)
        Control(LastBT).ForeColor = _RGB32(0, 0, 255)
        Control(ClearSearchBT).Disabled = False

        IF searchResultIndex = totalFound THEN
            Control(NextBT).Disabled = True
            Control(LastBT).Disabled = True
        END IF

        IF searchResultIndex = 1 THEN
            Control(FirstBT).Disabled = True
            Control(PreviousBT).Disabled = True
        END IF

        IF Searching THEN
            Caption(StatusBarLB) = "Buscando " + CHR$(34) + SearchString$ + CHR$(34) + " (" + LTRIM$(STR$(INT(SearchProgress / TotalRecords * 100))) + "%)..."
            IF totalFound THEN Caption(StatusBarLB) = Caption(StatusBarLB) + " | " + LTRIM$(STR$(totalFound)) + " encontrados."
        ELSE
            Caption(StatusBarLB) = "Busca por " + CHR$(34) + SearchString$ + CHR$(34) + " | Resultado " + LTRIM$(STR$(searchResultIndex)) + " de" + STR$(totalFound) + "."
        END IF
        Control(StatusBarLB).Redraw = True
    ELSE
        Control(FirstBT).ForeColor = _RGB32(0)
        Control(PreviousBT).ForeColor = _RGB32(0)
        Control(NextBT).ForeColor = _RGB32(0)
        Control(LastBT).ForeColor = _RGB32(0)
        Control(ClearSearchBT).Disabled = True

        IF CurrentRecord = TotalRecords THEN
            Control(NextBT).Disabled = True
            Control(LastBT).Disabled = True
        END IF

        IF CurrentRecord <= 1 THEN
            Control(FirstBT).Disabled = True
            Control(PreviousBT).Disabled = True
        END IF
    END IF
END SUB

SUB Refresh
    STATIC PrevRecord AS _UNSIGNED LONG

    WriteSetting "ControleDeOficios.ini", GenericUser + "-" + Usuario, "Ping", DATE$ + ", " + TIME$
    a$ = ReadSetting("ControleDeOficios.ini", "controle", "ForceQuitToUpdate")
    IF a$ = "True" THEN
        c$ = "updateOficios.exe"
        IF _COMMANDCOUNT THEN
            FOR i = 1 TO _COMMANDCOUNT
                d$ = COMMAND$(i)
                IF INSTR(d$, " ") > 0 THEN d$ = CHR$(34) + d$ + CHR$(34)
                c$ = c$ + " " + d$
            NEXT
        ELSE
            c$ = c$ + " -default"
        END IF
        SHELL _DONTWAIT c$
        __UI_BeforeUnload
    END IF

    IF inSearch THEN
        IF PrevRecord <> SearchResults(searchResultIndex) THEN
            Caption(UltimoOficioLB) = ReadSetting(file$, STR$(SearchResults(searchResultIndex)), "Numero")
            a$ = ReadSetting(file$, STR$(SearchResults(searchResultIndex)), "Descricao")
            b$ = ReadSetting(file$, STR$(SearchResults(searchResultIndex)), "Data")
            IF LEN(a$) > 0 AND LEN(b$) > 0 THEN
                a$ = b$ + ": " + a$
            ELSEIF LEN(a$) = 0 AND LEN(b$) > 0 THEN
                a$ = "Expedido em " + b$
            ELSEIF LEN(a$) = 0 AND LEN(b$) = 0 THEN
                a$ = "-"
            END IF
            Caption(UltimaDescricaoLB) = a$
            Caption(UltimoUsuarioLB) = ReadSetting(file$, STR$(SearchResults(searchResultIndex)), "Usuario")
            CurrentRecord = SearchResults(searchResultIndex)
            PrevRecord = CurrentRecord
        END IF
    ELSE
        a$ = ReadSetting(file$, "controle", "registros")
        IF VAL(a$) <> TotalRecords THEN
            IF CurrentRecord = TotalRecords THEN
                CurrentRecord = VAL(a$)
            END IF
            TotalRecords = VAL(a$)

            a$ = ReadSetting(file$, "controle", "UltimoNumero")
            Ultimo = VAL(LEFT$(a$, INSTR(a$, "/") - 1))
            Caption(UltimoOficioLB) = a$
            Proximo = Ultimo + 1

            Caption(BT) = LTRIM$(STR$(Proximo))
        END IF

        IF PrevRecord <> CurrentRecord THEN
            Caption(UltimoOficioLB) = _TRIM$(ReadSetting(file$, STR$(CurrentRecord), "Numero"))
            a$ = ReadSetting(file$, STR$(CurrentRecord), "Descricao")
            b$ = ReadSetting(file$, STR$(CurrentRecord), "Data")
            IF LEN(a$) > 0 AND LEN(b$) > 0 THEN
                a$ = b$ + ": " + a$
            ELSEIF LEN(a$) = 0 AND LEN(b$) > 0 THEN
                a$ = "Expedido em " + b$
            ELSEIF LEN(a$) = 0 AND LEN(b$) = 0 THEN
                a$ = "-"
            END IF
            Caption(UltimaDescricaoLB) = a$
            Caption(UltimoUsuarioLB) = ReadSetting(file$, STR$(CurrentRecord), "Usuario")
            PrevRecord = CurrentRecord
        END IF
    END IF

    __UI_ForceRedraw = True
    LastCheck = TIMER
END SUB

SUB __UI_BeforeUnload
    IF DoingLocalBackup THEN
        __UI_UnloadSignal = False
        _SCREENHIDE
        FinishBackupAndClose = True
    END IF
END SUB

SUB __UI_Click (id AS LONG)
    SELECT EVERYCASE id
        CASE CopyMenuCopy
            a$ = ReadSetting(file$, STR$(CurrentRecord), "Descricao")
            IF LEN(a$) THEN _CLIPBOARD$ = a$
        CASE MenuItem1
            __UI_BeforeUnload
        CASE MenuItem2
            Answer = MessageBox("Controle de Ofícios - TJMG\nComarca de Espera Feliz\n(c) Fellippe Heitor, 2018-2020", "", MsgBox_OkOnly + MsgBox_Information)
        CASE BT
            Answer = MessageBox("Confirma?", "", MsgBox_YesNo + MsgBox_Question)
            _DELAY .1: _KEYCLEAR
            IF Answer = MsgBox_Yes THEN
                a$ = LTRIM$(STR$(Proximo)) + "/" + LTRIM$(STR$(EsteAno))
                _CLIPBOARD$ = a$
                nextRecord$ = LTRIM$(STR$(TotalRecords + 1))
                WriteSetting file$, nextRecord$, "Numero", a$
                WriteSetting file$, nextRecord$, "Data", MID$(DATE$, 4, 2) + "/" + LEFT$(DATE$, 2) + "/" + RIGHT$(DATE$, 4)
                WriteSetting file$, nextRecord$, "Usuario", Usuario
                IF LEN(Text(DescricaoTB)) THEN WriteSetting file$, nextRecord$, "Descricao", Text(DescricaoTB)
                WriteSetting file$, "controle", "UltimoNumero", a$
                WriteSetting file$, "controle", "registros", nextRecord$

                CurrentRecord = TotalRecords + 1
                inSearch = False
                REDIM SearchResults(1 TO TotalRecords + 1) AS _UNSIGNED LONG

                IF Primeiro = 0 THEN Primeiro = CurrentRecord
                Refresh
                Text(DescricaoTB) = ""
                __UI_Focus = DescricaoTB
                DoLocalBackup = True
            END IF
        CASE ClearSearchBT
            inSearch = False
            SetFocus DescricaoTB
            Refresh
        CASE VerHistoricoBT, MenuItem3
            SHELL _HIDE _DONTWAIT "start " + file$
        CASE UltimaDescricaoLB
            Control(UltimaDescricaoTB).Hidden = False
            Text(UltimaDescricaoTB) = ""
            SetFocus UltimaDescricaoTB
        CASE UltimoUsuarioLB
            Control(UltimoUsuarioTB).Hidden = False
            Text(UltimoUsuarioTB) = ""
            SetFocus UltimoUsuarioTB
        CASE FirstBT
            IF inSearch THEN searchResultIndex = 1 ELSE CurrentRecord = 1
        CASE PreviousBT
            IF inSearch THEN
                searchResultIndex = searchResultIndex + (searchResultIndex - 1 >= 1)
            ELSE
                CurrentRecord = CurrentRecord + (CurrentRecord - 1 >= 1)
            END IF
        CASE NextBT
            IF inSearch THEN
                searchResultIndex = searchResultIndex - (searchResultIndex + 1 <= totalFound)
            ELSE
                CurrentRecord = CurrentRecord - (CurrentRecord + 1 <= TotalRecords)
            END IF
        CASE LastBT
            IF inSearch THEN searchResultIndex = totalFound ELSE CurrentRecord = TotalRecords
        CASE FirstBT, PreviousBT, NextBT, LastBT
            LastCheck = 0
            Refresh
            Control(UltimoOficioTB).Hidden = True
            Control(UltimaDescricaoTB).Hidden = True
        CASE UltimoOficioLB
            Control(UltimoOficioTB).Hidden = False
            Text(UltimoOficioTB) = _TRIM$(ReadSetting(file$, STR$(CurrentRecord), "Numero"))
            SetFocus UltimoOficioTB
    END SELECT
END SUB

SUB __UI_MouseEnter (id AS LONG)
    SELECT CASE id
        CASE ControleDeOficios

        CASE Frame1

        CASE PrximoNmero

        CASE ltimoUtilizadoLB

        CASE UltimoOficioLB

        CASE Label2

        CASE UltimaDescricaoLB
            a$ = ReadSetting(file$, STR$(CurrentRecord), "Descricao")

            IF LEN(a$) THEN
                ToolTip(UltimaDescricaoLB) = "Botáo direito para copiar"
            ELSE
                ToolTip(UltimaDescricaoLB) = ""
            END IF
        CASE Label3

        CASE UltimoUsuarioLB

        CASE Label4

        CASE UsuarioAtualLB

        CASE DecriaoOpcionalLB

        CASE DescricaoTB

        CASE BT

    END SELECT
END SUB

SUB __UI_MouseLeave (id AS LONG)
    SELECT CASE id
        CASE ControleDeOficios

        CASE Frame1

        CASE PrximoNmero

        CASE ltimoUtilizadoLB

        CASE UltimoOficioLB

        CASE Label2

        CASE UltimaDescricaoLB

        CASE Label3

        CASE UltimoUsuarioLB

        CASE Label4

        CASE UsuarioAtualLB

        CASE DecriaoOpcionalLB

        CASE DescricaoTB

        CASE BT

    END SELECT
END SUB

SUB __UI_FocusIn (id AS LONG)
    SELECT CASE id
        CASE DescricaoTB

        CASE BT

    END SELECT
END SUB

SUB __UI_FocusOut (id AS LONG)
    SELECT CASE id
        CASE DescricaoTB

        CASE BT

        CASE UltimoOficioTB, UltimaDescricaoTB, UltimoUsuarioTB
            Control(id).Hidden = True
            Control(id).Redraw = True
    END SELECT
END SUB

SUB __UI_MouseDown (id AS LONG)
    SELECT CASE id
        CASE ControleDeOficios

        CASE Frame1

        CASE PrximoNmero

        CASE ltimoUtilizadoLB

        CASE UltimoOficioLB

        CASE Label2

        CASE UltimaDescricaoLB

        CASE Label3

        CASE UltimoUsuarioLB

        CASE Label4

        CASE UsuarioAtualLB

        CASE DecriaoOpcionalLB

        CASE DescricaoTB

        CASE BT

    END SELECT
END SUB

SUB __UI_MouseUp (id AS LONG)
    SELECT CASE id
        CASE ControleDeOficios

        CASE Frame1

        CASE PrximoNmero

        CASE ltimoUtilizadoLB

        CASE UltimoOficioLB

        CASE Label2

        CASE UltimaDescricaoLB

        CASE Label3

        CASE UltimoUsuarioLB

        CASE Label4

        CASE UsuarioAtualLB

        CASE DecriaoOpcionalLB

        CASE DescricaoTB

        CASE BT

    END SELECT
END SUB

SUB __UI_KeyPress (id AS LONG)
    IF __UI_KeyHit = 27 AND inSearch = True THEN inSearch = False: SetFocus DescricaoTB: Refresh

    SELECT EVERYCASE id
        CASE DescricaoTB

        CASE BT

        CASE UltimoOficioTB
            term$ = "Numero"
        CASE UltimoUsuarioTB
            term$ = "Usuario"
        CASE UltimaDescricaoTB
            term$ = "Descricao"
        CASE UltimoOficioTB, UltimoUsuarioTB, UltimaDescricaoTB
            IF __UI_KeyHit = -13 THEN
                DIM tempSearchString$, i AS LONG, j AS LONG
                REDIM Element$(0), totalElements AS LONG, readingElement AS _BYTE

                SearchString$ = ""
                tempSearchString$ = _TRIM$(Text(id))
                Control(id).Hidden = True
                IF LEN(tempSearchString$) THEN
                    inSearch = True 'updates status bar
                    Searching = True
                    FOR i = 1 TO LEN(tempSearchString$)
                        IF ASC(tempSearchString$, i) = 37 THEN
                            IF RIGHT$(SearchString$, 1) <> "%" THEN
                                SearchString$ = SearchString$ + MID$(tempSearchString$, i, 1)
                            END IF
                        ELSE
                            SearchString$ = SearchString$ + MID$(tempSearchString$, i, 1)
                        END IF
                    NEXT

                    __UI_WaitMessage = "Procurando por '" + SearchString$ + "' - ESC para cancelar"

                    'Separate search elements into an array
                    FOR i = 1 TO LEN(SearchString$)
                        IF ASC(SearchString$, i) <> 37 THEN
                            IF NOT readingElement THEN
                                readingElement = True
                                totalElements = totalElements + 1
                                REDIM _PRESERVE Element$(1 TO totalElements)
                                Element$(totalElements) = Element$(totalElements) + UCASE$(MID$(SearchString$, i, 1))
                            ELSE
                                Element$(totalElements) = Element$(totalElements) + UCASE$(MID$(SearchString$, i, 1))
                            END IF
                        ELSE
                            readingElement = False
                        END IF
                    NEXT

                    'Search through records...
                    DIM k&
                    totalFound = 0
                    searchResultIndex = 1
                    FOR SearchProgress = 1 TO TotalRecords
                        k& = _KEYHIT
                        IF k& = 27 THEN totalFound = 0: EXIT FOR
                        a$ = UCASE$(ReadSetting(file$, STR$(SearchProgress), term$))
                        IF totalElements = 1 THEN
                            IF (INSTR(SearchString$, "%") = 0 AND a$ = Element$(1)) OR _
                               (LEFT$(SearchString$, 1) = "%" AND RIGHT$(SearchString$, 1) <> "%" AND RIGHT$(a$, LEN(Element$(1))) = Element$(1)) OR _
                               (LEFT$(SearchString$, 1) <> "%" AND RIGHT$(SearchString$, 1) = "%" AND LEFT$(a$, LEN(Element$(1))) = Element$(1)) OR _
                               (LEFT$(SearchString$, 1) = "%" AND RIGHT$(SearchString$, 1) = "%" AND INSTR(a$, Element$(1)) > 0) THEN
                                totalFound = totalFound + 1
                                IF totalFound = 1 THEN CurrentRecord = SearchProgress
                                SearchResults(totalFound) = SearchProgress
                            END IF
                        ELSE
                            DIM lastPosition AS LONG, found AS _BYTE
                            lastPosition = 0
                            found = True
                            FOR j = 1 TO totalElements
                                IF j = 1 THEN
                                    'first element
                                    IF LEFT$(SearchString$, 1) = "%" THEN
                                        lastPosition = INSTR(lastPosition + 1, a$, Element$(j))
                                        IF lastPosition = 0 THEN found = False: EXIT FOR
                                    ELSE
                                        IF LEFT$(a$, LEN(Element$(j))) <> Element$(j) THEN found = False: EXIT FOR
                                    END IF
                                ELSEIF j = totalElements THEN
                                    'last element
                                    IF RIGHT$(SearchString$, 1) = "%" THEN
                                        lastPosition = INSTR(lastPosition + 1, a$, Element$(j))
                                        IF lastPosition = 0 THEN found = False: EXIT FOR
                                    ELSE
                                        IF RIGHT$(a$, LEN(Element$(j))) <> Element$(j) THEN found = False: EXIT FOR
                                    END IF
                                ELSE
                                    'middle elements
                                    lastPosition = INSTR(lastPosition + 1, a$, Element$(j))
                                    IF lastPosition = 0 THEN found = False: EXIT FOR
                                END IF
                            NEXT
                            IF found THEN
                                totalFound = totalFound + 1
                                IF totalFound = 1 THEN CurrentRecord = SearchProgress
                                SearchResults(totalFound) = SearchProgress
                            END IF
                        END IF
                        __UI_DoEvents
                    NEXT

                    IF totalFound > 0 THEN
                        inSearch = True
                    ELSE
                        inSearch = False
                        IF k& <> 27 THEN
                            Answer = MessageBox("Nenhum resultado encontrado", "", MsgBox_OkOnly + MsgBox_Critical)
                        END IF
                    END IF
                END IF
                Searching = False
                Refresh
                __UI_KeyHit = 0
            ELSEIF __UI_KeyHit = 27 THEN
                Control(id).Hidden = True
                SetFocus DescricaoTB
                __UI_ForceRedraw = True
            END IF
    END SELECT
END SUB

SUB __UI_TextChanged (id AS LONG)
    SELECT CASE id
        CASE DescricaoTB

    END SELECT
END SUB

SUB __UI_ValueChanged (id AS LONG)
    SELECT CASE id
    END SELECT
END SUB

SUB __UI_FormResized

END SUB
